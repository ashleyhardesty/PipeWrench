; Name: Xlat2HTML.pip
; 
; This pipe was used to translate the original text version of the 
; Pyper manual (Pyper Commands.txt) to the HTML version (index.htm).  
; The only thing that this pipe didn't handle was adding the filter 
; list at the top of the resulting manual.  That was created using 
; the pipe, XlatFilterList.pip
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; First, fix some minor editing errors 
; that got carried over from the past:

ReplStr 'Base2Dec' 'BaseToDec' 'Dec2Base' 'DecToBase'

; Substitute any special chars:

ReplStr '<' '&lt;' '>' '&gt;'

; Translate all text into paragraphs:

TrimLinesRight
DelExtraBlankLines
InsStr 1 '<p>'
AppendStr '</p>'
ReplStr '<p></p>' ''

; Prepare to isolate/edit lines:

JoinLines /p
ReplStr '</p><p>' '<br><EoL>'
InsStr 1 ' '
InsLineNo /w4 /z

; Change the font of any paragraph that contains "----+----":

GroupLines '----+----' 
IsolateLines '----+----'
   InsStr 9 '<font size="1" face="Lucida Console" color="gray">'
   ReplStr '</p>' '</font></p>'
EndIsolate

; Change the font of any paragraph that contains the marker, 
; "bwb" (which I had added on a line by itself at the top of
; each paragraph requiring this font change):

GroupLines 'bwb'
IsolateLines 'bwb'
   InsStr 21 '<font size="1" face="Lucida Console" color="gray">'
   ReplStr '</p>' '</font></p>'
   DelChars 9 12
EndIsolate

; Return the text to normal:

SortLines
DelChars 1 5
ReplStr '<EoL>' '\e'

; Replace multiple spaces with non-break spaces:

ReplStr '  ' '&nbsp;&nbsp;'

; Change the main heading:

ReplStr '<p>Pyper''s Core Text Filters</p>' '<h2>Pyper''s Core Text Filters</h2>'

; Change the first line of each topic to a heading:

AppendStr '<EoL>'
JoinLines
ReplStr '<p>(\\w+)</p><EoL><EoL><p>syntax:' '<a name="$1"><h3>$1</h3></a><EoL><EoL><p>syntax:' /r
ReplStr '<EoL>' '\e'

; Change the font of the "syntax" and "summary" lines:

ReplStr '<p>syntax:\\s(.+?)<br>' '<p><font size="1">syntax: </font><font face="Lucida Console" size="1" color="gray">$1</font><br>' /r
ReplStr 'summary:\\s(.+?)</p>' '<font size="1">summary: </font><font size="1" color="gray">$1</font></p>' /r
